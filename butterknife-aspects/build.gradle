apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        String variantName = "${variant.name}".capitalize();
        task("${variant.name}Javadoc", type: Javadoc, dependsOn: "assemble${variantName}") {
            source = variant.javaCompile.source

            title = "ButterKnife aspects"

            options.links("http://docs.oracle.com/javase/7/docs/api/");
            options.linksOffline("http://d.android.com/reference","${android.sdkDirectory}/docs/reference");

            // First add all of your dependencies to the classpath, then add the android jars
            classpath += files(variant.javaCompile.classpath.files)
            classpath += files(android.getBootClasspath())

            // We're excluding these generated files
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }
    }

    libraryVariants.all { variant ->
        task("${variant.name}JavadocJar", type: Jar, dependsOn: "${variant.name}Javadoc") {
            classifier = 'javadoc'
            from tasks["${variant.name}Javadoc"].destinationDir
        }

        // Add the Javadoc jar to the project's artifacts. This will allow us to upload it easily later
        project.artifacts.add("archives", tasks["${variant.name}JavadocJar"]);
    }
}

apply from: 'build-scripts/gradle-mvn-push.gradle'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile (
            'com.android.support:appcompat-v7:22.2.0',
            "com.shazam.android:aspects:1.0.0@aar",
            "com.jakewharton:butterknife:7.0.1",
    )
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'butterknife-aspects'

    publishedGroupId = 'com.savvasdalkitsis'
    libraryName = 'butterknife-aspects'
    artifact = 'butterknife-aspects'

    libraryDescription = 'An aspect for the popular Android library Butterknife'

    siteUrl = 'https://github.com/savvasdalkitsis/butterknife-aspects'
    gitUrl = 'https://github.com/savvasdalkitsis/butterknife-aspects.git'

    libraryVersion = '1.0.0'

    developerId = 'savvasdalkitsis'
    developerName = 'Savvas Dalkitsis'
    developerEmail = 'savvas.dalkitsis@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply from: '../scripts/installv1.gradle'
apply from: '../scripts/bintrayv1.gradle'
